#+ros.init
(roswell:include '("util" "system") "util-template")
#+quicklisp
(ql:quickload '("uiop" "djula") :silent t)
(defpackage :roswell.util.template
  (:use :cl)
  (:export
   :*template-function-plist*
   :*template-base-directories*

   :template-path
   :template-file-path
   :template-asd-path

   :templates-list
   :template-create
   :template-directory
   :template-remove-file
   :template-add-file
   :template-attr-file

   :template-apply

   #+ros.init :template-default))

(in-package :roswell.util.template)

(defvar *template-base-directories* '())
(defvar *template-function-plist*
  '(:djula djula
    :copy copy-file))

(defun run-n-get-first-result (cmds)
  (loop for cmd in cmds
        for result = (ignore-errors (uiop:run-program cmd :output :string))
        when result
          return (remove #\Newline result)))

(defvar *author* nil)
(defun author ()
  (or *author*
      (setf *author* (run-n-get-first-result '("git config --global --get user.name"
                                               "whoami")))))

(defvar *email* nil)
(defun email ()
  (or *email*
      (setf *email* (run-n-get-first-result '("git config --global --get user.email"
                                              "echo $(whoami)@$(hostname)")))))

(defun sanitize (name)
  (remove-if (lambda (x) (find x "./\\")) name))

(defun enc-string (str)
  (format nil "扯蟒祜镳骘徙蝻篌篝泔祆邈ㄣ栳颦泔溴椹┅ㄤ彐躅溴悱篝蜷铉篝颟鏖翳轭瘐舡骝镯篝蜷铉篚怏糸趱翦＼羽徙＼篝颟ㄣ镥蜚祜镳鏖翳蝈徜忉箦扯鏖翳蝈徜弼犰骘蝈徜铋铋飑麒殪泔祆邈ㄣ镤瀛汨狎椹篝蜷铉┅ㄤ彐躅翦眇灬翦蟓扉篝é脲骈祠弪钺礤戾舄è祜镳骘轭ㄡ痧孱翦眇灬翦忉箦溟蝈泗矧殄螵扉篝ㄦ轵篝耢邯祜汜飙痱镪邈舡溟蝈泗矧殄螵┅狃疱钿ㄤ轵邈麸蝙礤蜱瀛疳翳钺礤狍洧┅┅í蝈盹鲥殒铒灬礅溽ㄩ珙矧瀛弪蝻蝮篝蜷铉羼踽Ⅱ矬麇祆轭轸疳翳钺礤钺礤哄钿背┅┅íㄣ镱礤蜱瀛疳翳钺礤㈤铋舡溴驷蹯舢扉箴蝻蠛镳㈧轶痄轵┅┅íㄩ骈祠弪戾è骈祠弪筢铋糸骈祠弪┅蝈盹鲥殒铒灬礅溽ㄦ轭疳翳钺礤钺礤扉篝ㄦ矧磲铋Ⅱ矬麇祆轭轸立骈祠弪ㄦ矧磲铋㈤铋舡立骈祠弪┅呼弩у聃犰┅┅┅ㄣ镱钺礤磲疸狎灬礅溽篚怏羼疳翳钺礤钺礤ū痫箝糸镱殒灬礅溽ㄦ轭┅疳翳钺礤钺礤烘蝻憝孱舂┅┅┅┅ㄤ彐躅翦眇灬翦疳翳钺礤戾è骘躅翦眇灬翦蟓扉篝烘殪翦钺礤┅ㄩ骘躅磲脲疳翳钺礤呼疱铋侯犴铋轰彐狨祠ㄦ轵篝骘躅洎礤蜱瀛疳翳钺礤ㄦ矧磲铋Ⅳ屙痨狒弩怜筢铋糸钺礤┅ㄦ轵篝耢邯祜汜飙痱镪邈舡溟蝈泗矧殄螵┅┅ㄤ彐躅翦眇灬翦骈戾疳翳翦眇灬翦钺礤疳翳礤蜱瀛疳翳钺礤ㄥ钽篝蜷铉疳翳礤蜱瀛疳翳钺礤ㄦ矧磲铋镰翦眇灬翦翦眇灬翦钺礤翦眇灬翦疳翳翦眇灬翦钺礤┅┅ㄤ彐躅翦眇灬翦狍洵疳翳翦眇灬翦钺礤磲脲疳翳钺礤轰彐狨祠翦眇灬翦疳翳翦眇灬翦钺礤呼疱⑨箐侯犴ㄦ矧磲铋Ⅱ矬麇祆轭轸立翦眇灬翦钺礤┅ㄤ彐躅狃痨潢蹯翦眇灬翦篝蜷铉篝蝈犴疳蜥眢ㄡ痧禊т牾灬汉蝈钿弪翦眇灬翦ㄤ牾灬汉泔眇殪瀛篝蜷铉翦眇灬翦篝蜷铉篝蝈犴啜鲤狎犴横豸栾ㄡ豸栾颟哄磲殪ㄥ磲殪乎铋鲥蝮犰唪轫ㄧ弭躅轹弪筢飙糸礤┅┅ㄤ彐躅泔瘗骈戾篁溴篝疳蜥眢ㄤ邈灬蝈ㄩ珙矧疳蜥眢┅麒孱痱镡瀛骈戾溴篝ㄥ蝌矧㈡殪屮轸立溴篝┅蹰镳恒镳骈戾篁溴篝┅ㄤ彐躅潢蹯篁溴篝疳蜥眢鏖翳镳孱骈戾溴篝轰轵邈糸镱猴豸瘐舂ㄡ痧禊潢蹯蹰镳候遽洵骈戾篝蜷铉篁悌疳蜥眢┅ㄤ彐躅翦眇灬翦狃痨翦眇灬翦钺礤狎珞轭骘脲疳翳溴驷蹯舡疳翳钺礤溴驷蹯趔┅ㄤ邈灬蝈ㄩ珙矧徕戾翦眇灬翦钺礤狎珞轭骘┅换翕徭翦颦栾镫ㄦ戾è镢翎篝蜷铉疳蝮瀛轭翦珏篝蜷铉候徜轼俯脲篝蜷铉麒孱篝蜷铉戾í蝈徜弼犰蝈徜骝镯篝蜷铉ㄦ矧磲铋⒑立篝蜷铉┅┅┅箦翩狎珞啜侯犴ㄦ轵篝狎珞括祜镳骘镱蝈篝狎珞怡＇沅潋麒殪篝蜷铉羼踽ㄦ轵篝椹哄钿博泔祆邈脲篚怏羼ㄦ轵篝椹博泔祆邈箦泔钿椹┅祜镳骘轭ㄧ弭轭骘烘殪弩骘骝镯翦眇灬翦骈戾疳翳翦眇灬翦钺礤ㄧ弭侯犴濠骘麸ㄥ铙躜瀛溟蝈泗矧殄蟓屮轶礤蜱瀛疳翳钺礤ㄩㄧ弭候鬻蜷翦ㄡ痧禊潢蹯ㄧ弭候鬻蜷翦铋狎珞ㄧ弭侯犴濠疳翳┅滹躅戾篌ㄥ聃犰ㄦ殪瀛钺礤篝蜷铉麸ㄦ躅汜祆ㄧ弭翦眇灬翦骢钽糸镱痨轶舄脲ㄧ弭喉弭栾洎┅骝镯麸狎珞┅麒孱ㄩ珙矧瀛弪蝻蝮镢翎ㄧ弭恒桧镤┅＋筲沆筲痫箝恒桧镤麸镢翎ㄧ弭恒桧镤┅┅┅ㄤ彐躅翦眇灬翦黩轸钺礤扉篝鏖翳镳孱骈戾ㄥ铙躜瀛溟蝈泗矧殄蟓屮轶翦眇灬翦狍洵疳翳钺礤┅轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴洪姝滹弩铒舡屮轶恒蝈狒濠戾è疳汶徵蝈徜骝镯篝蜷铉ㄦ矧磲铋⒑蝻篦屐飚轭轸立钺礤┅í疳汶徵濯ㄦ轭洵疳汶徵候矬麇祆豸殪翦眇灬翦┅ㄦ矧磲誉"
              `((defpackage ,package
                  (:use :cl))
                (in-package ,package)
                (defvar *params* '(,@list))
                (defun ,(read-from-string name) (_ &rest r)
                  (asdf:load-system :roswell.util.template :verbose nil)
                  (funcall (read-from-string "roswell.util.template:template-apply") _ r *params*)))))))

(defun template-read (name)
  (let (package
        read/)
    (with-open-file (o (template-asd-path name)
                       :direction :input
                       :if-does-not-exist :error)
      (setq read/ (read o))
      (unless (and
               (eql 'defpackage (first read/))
               (string-equal "ROSWELL.INIT." (symbol-name (setq package (second read/))) :end2 13))
        (error "not init template A%" read/))
      (setq read/ (read o))
      (unless (and
               (eql 'in-package (first read/))
               (eql (second read/) package))
        (error "not init template A%" read/))
      (setq read/ (read o))
      (unless (and (eql 'defvar (first read/))
                   (string-equal  '*params* (second read/))) 
        (error "not init template S%" (list read/)))
      (second (third read/)))))

(defun template-create (name)
  (template-write (sanitize name) nil))

(defun template-directory (name)
  (getf (template-read (sanitize name)) :files))

#+ros.init
(defun (setf template-default) (template-name)
  (setf (roswell.util:config "init.default") (sanitize template-name)))

#+ros.init
(defun template-default (&optional (default "default"))
  (or (roswell.util:config "init.default") (sanitize default)))

(defun template-add-file (template-name file-name path-copy-from)
  (let ((template-name (sanitize template-name))
        (info (template-read template-name)))
    (uiop:copy-file path-copy-from
                    (ensure-directories-exist (template-file-path template-name file-name)))
    (unless (find file-name (getf info :files) :key (lambda (x) (getf x :name)) :test 'equal)
      (push (list :name file-name :method "copy") (getf info :files)))
    (template-write template-name info)))

(defun template-remove-file (template-name file-name)
  (let* ((template-name (sanitize template-name))
         (info (template-read template-name)))
    (uiop:delete-file-if-exists
     (template-file-path template-name file-name))
    (when (find file-name (getf info :files) :key (lambda (x) (getf x :name)) :test 'equal)
      (setf (getf info :files)
            (remove file-name (getf info :files)
                    :key (lambda (x) (getf x :name)) :test 'equal))
      (template-write template-name info))))

(defun template-attr-file (template-name file-name key value)
  (let* ((template-name (sanitize template-name))
         (info (template-read template-name))
         (found (find file-name (getf info :files) :key (lambda (x) (getf x :name)) :test 'equal)))
    (when found
      (if (eql (getf found key #1='#:a) #1#)
          (setf (cdr (last found)) (list key value))
          (setf (getf found key ) value))
      (template-write template-name info))))

#+ros.init
(roswell.util:system "util-template")

#!/bin/sh
#|-*- mode:lisp -*-|#
#|Delete installed implementations
exec ros +Q -m roswell -L sbcl-bin -- $0 "$@"
|#

(when (cl:find-package :ros.sub.delete)
  (push :ros.sub.delete *features*))

#-ros.util
(load (make-pathname
       :defaults *load-pathname*
       :name "util" :type "lisp"))

#-ros.sub.delete
(defpackage :ros.sub.delete
  (:use :cl :ros.util))
(in-package :ros.sub.delete)
#-ros.sub.delete
(ros:quicklisp :environment nil)

#-ros.sub.delete
(unless (find-package :uiop)
  (ql:quickload :uiop :silent t))

#-ros.sub.delete
(progn
  (defun delete-dump (params)
    (declare (ignorable params))
    (let ((path (make-pathname :name (second params)
                               :type "core"
                               :defaults
                               (merge-pathnames (format nil "impls/~A/~A/~A/dump/"
                                                        (uname-m) (uname) (first params))
                                                (homedir)))))
      (if (probe-file path)
          (delete-file path)
          (format *error-output* "The specified image doesn't exist \"~A ~A\"~%" (first params) (second params)))))

  (defun main (subcmd cmd &rest r)
    (let* ((default (config "default.lisp"))
           (verstring (format nil "~A.version" default))
           (config (format nil "~A/~A" default (config verstring))))
      ;; (print (list subcmd cmd r default verstring config))
      (cond
        ((null r)
         (format *error-output* "Usage: ros delete dump [IMAGES...]~%")
         (format *error-output* "Usage: ros delete [IMPLS...]~2%")
         (format *error-output* "Possible subcommands:~%")
         (finish-output *error-output*)
         (format t "dump~%")
         (finish-output)
         (format *error-output* "~%Possible candidates of installed impls for deletion:~2%")
         (finish-output *error-output*)
         (ros:roswell '("list" "installed") *standard-output*))
        ((not (equal subcmd "main"))
         (error "what is this?"))
        ((equal (first r) "dump")
         (delete-dump (rest r)))
        (t
         (let* ((impl-name (first r))
                (path (merge-pathnames (format nil "impls/~A/~A/~A/"
                                               (uname-m) (uname) impl-name)
                                       (homedir))))
           (unless (position :up (pathname-directory path))
             (cond ((probe-file path)
                    (uiop/filesystem:delete-directory-tree path :validate t)
                    (format t "~&~A was deleted successfully.~%" impl-name)
                    (when (equal impl-name config)
                      (format t "~&clear config ~S~%" verstring)
                      (setf (config verstring) "")))
                   (t (format *error-output* "~&~A is not installed yet.~%" impl-name))))))))))

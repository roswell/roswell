#!/bin/sh
#|-*- mode:lisp -*-|#
#|Create new ros script
exec ros +Q -m roswell -L sbcl-bin -- $0 "$@"
|#
(when (cl:find-package :ros.sub.init)
  (push :ros.sub.init *features*))
#-ros.sub.init
(defpackage :ros.sub.init
  (:use :cl))
(in-package :ros.sub.init)

#-ros.sub.init
(ros:quicklisp :environment nil)
#-ros.sub.init ;; sbcl-bin is old enough not to include UIOP on some OSes like Mac OS X.
(unless (find-package :uiop)
  (ql:quickload :uiop :silent t))

#-ros.sub.init
(defun main (subcmd cmd &optional name &rest r)
  (declare (ignore cmd))
  (cond ((and (equal subcmd "main") name)
         (let (script-path
               template
               (date (get-universal-time)))
           (when r (setq template name
                         name (first r)
                         r (cdr r)))
           (setq script-path (make-pathname :defaults name :type "ros"))
           (if template
               (progn
                 (ql:quickload template :silent t)
                 (when ros:*main*
                   (apply ros:*main* r)))
               (handler-case
                   (unless (prog1
                               (with-open-file (out script-path
                                                    :direction :output
                                                    :if-exists nil
                                                    :if-does-not-exist :create)
                                 (when out
                                   (format out "~@{~A~%~}"
                                           "#!/bin/sh"
                                           "#|-*- mode:lisp -*-|#"
                                           "#|" "exec ros -Q -- $0 \"$@\"" "|#"
                                           (format nil "(defpackage :ros.script.~A.~A" name date)
                                           "  (:use :cl))"
                                           (format nil "(in-package :ros.script.~A.~A)" name date)
                                           "(defun main (&rest argv)"
                                           "  (declare (ignorable argv)))")
                                   (format t "~&Successfully generated: ~A~%" script-path)
                                   t))
                             #+sbcl (sb-posix:chmod script-path #o700))
                     (format *error-output* "~&File already exists: ~A~%" script-path)
                     (uiop:quit -1))
                 (error (e)
                   (format *error-output* "~&~A~%" e)
                   (uiop:quit -1))))))))

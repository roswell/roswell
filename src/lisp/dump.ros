#!/bin/sh
#|-*- mode:lisp -*-|#
#|Dump image for faster startup or Make Executable
exec ros -- $0 "$@"
|#

(defpackage :ros.dump
  (:use :cl))
(in-package :ros.dump)

(unless (find-package :uiop)
  #-quicklisp(require :uiop)
  #+quicklisp(ql:quickload :uiop :silent t))

(defun homedir ()
  (ros:opt "homedir"))

(defun uname ()
  (ros:roswell '("roswell-internal-use uname") :string t))

(defun uname-m ()
  (ros:roswell '("roswell-internal-use uname -m") :string t))

(defun dump-dir ()
  (merge-pathnames (format nil "impls/~A/~A/~A/dump/"
                           (uname-m) (uname) (ros:opt "impl"))
                   (homedir)))

(defun dump-output (params &optional force)
  (flet ((dump (path &optional mode)
           (if (and (not force)
                    (probe-file path))
               (if (eql mode :normal)
                   (format t "dump ~s already exists~%" (pathname-name path))
                   (format t "file ~s already exists~%" path))
               (progn
                 #+sbcl
                 (sb-ext:save-lisp-and-die path)
                 #+ccl
                 (ccl:save-application path)))))
    (cond
      ((null params)
       (format t "Usage: ~A [OPTIONS] dump output [-f] [-o] name~%"
               (ros:opt "argv0")))
      ((equal "-f" (first params))
       (dump-output (rest params) t))
      ((equal "-o" (first params))
       (if (> (length params) 1)
           (dump (second params))
           (format t "you should specify a filename where the image is dumped.~%")))
      (t (let ((path (merge-pathnames (format nil "~A.core"
                                              (first params))
                                      (dump-dir))))
           (dump (ensure-directories-exist path) :normal))))))

(defun dump-executable (params)
  (if params
      (let ((cmds (let ((*read-eval*))
                    (read-from-string
                     (format nil "(~A)"
                             (ros:opt "restart")))))
            (out (first params))
            (actual-out (or (and (equal (second params) "-o") ;; skimped ...
                                 (third params))
                            (first params))))
        (unless cmds
          (warn "dumping an executable without specifing the initial behaviour."))
        (when (equalp (pathname-type out) "ros")
          (let ((*package* (find-package :cl-user))
                main-list)
            (ros:script nil out)
            (setf main-list (let ((*package* (find-package :keyword)))
                              `((:entry ,(format nil "~S" ros:*main*))))
                  out (make-pathname :type #-win32 nil #+win32 "exe" :defaults actual-out)
                  cmds (if (first cmds)
                           (append cmds main-list)
                           main-list))))
        #+sbcl
        (sb-ext:save-lisp-and-die
         out
         :toplevel
         #'(lambda ()
             (setf ros:*argv* (rest sb-ext:*posix-argv*))
             (ros:run cmds))
         :executable t)
        #+ccl
        (ccl:save-application
         out
         :toplevel-function
         #'(lambda ()
             (setf ros:*argv* (rest (ccl::command-line-arguments)))
             (ros:run cmds))
         :prepend-kernel t))
      (format t "ros dump executable help~%")))

(defvar *subcmds*
  '(("output" "Create dump" dump-output)
    #+(or sbcl ccl)("executable" "Create an executable" dump-executable)))

(defun main (subcmd cmd &rest r)
  (cond ((and (equal subcmd "main") r)
         (let ((func (third (assoc (first r) *subcmds* :test 'equal))))
           (if func
               (funcall func (rest r))
               (format t "'~A' is not a valid command for '~A' subcommand~%" (first r) cmd))))
        ((or (equal subcmd "help") (not r))
         (format t "Usage: ~A [OPTIONS] ~A SUBCMD [ARGS]~%"
                 (ros:opt "argv0") cmd)
         (loop :with len := (1+ (loop :for (cmd . nil) :in *subcmds* :maximize (length cmd)))
            :for (cmd desc nil) :in *subcmds*
            :do (format t "  ~VA ~A~%" len cmd desc)))))
